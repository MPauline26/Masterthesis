ODS GRAPHICS ON;

DATA TRAIN_SAMPLE;
SET final.TRAIN_SAMPLE;
RUN;

DATA TEST_SAMPLE;
SET final.TEST_SAMPLE;
RUN;

PROC LOGISTIC DATA = TRAIN_SAMPLE DESCENDING;
MODEL DEFAULT_12M =
	fico
	dti
	cltv
	cnt_borr
	flag_fthb
	flag_mi
	flag_orig_loan_term_HEQ_360M
	channel__9
	channel__B
	channel__C
	channel__T
	loan_purpose__C
	loan_purpose__N
	cd_ppty_val_type__1
	cd_ppty_val_type__3
	cd_ppty_val_type__9
	us_reg__Midwest
	us_reg__Northeast
	us_reg__Other
	us_reg__West

/* REMOVED DUE TO HIGH CORRELATION */
/*	cltv_ADJ, ltv, ltv_ADJ */

/* NOT INCLUDED DUE TO LINEAR COMBINATION */
/*	channel__R, loan_purpose__P, cd_ppty_val_type__2, us_reg__South */

/ SELECTION = STEPWISE SLSTAY=0.15 SLENTRY=0.15 STB OUTROC=MODELROC;
SCORE DATA=TRAIN_SAMPLE OUT = LOGIT_TRAINING FITSTAT OUTROC=TROC;
SCORE DATA=TEST_SAMPLE OUT = LOGIT_TEST FITSTAT OUTROC=VROC;
ROC;
ODS OUTPUT ROCASSOCIATION = AUC_MODEL;
RUN;

/*AN ENTRY SIGNIFICANCE LEVEL OF 0.15, SPECIFIED IN THE SLENTRY=0.15 OPTION, MEANS A*/
/*VARIABLE MUST HAVE A P-VALUE < 0.15 IN ORDER TO ENTER THE MODEL REGRESSION.*/
/*AN EXIT SIGNIFICANCE LEVEL OF 0.15, SPECIFIED IN THE SLSTAY=0.15 OPTION, MEANS */
/*A VARIABLE MUST HAVE A P-VALUE > 0.15 IN ORDER TO LEAVE THE MODEL*/

/* KS STATISTICS*/
PROC NPAR1WAY DATA=LOGIT_TEST EDF;
CLASS DEFAULT_12M;
VAR P_1;
RUN;
