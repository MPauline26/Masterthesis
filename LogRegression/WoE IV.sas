
%MACRO WOE_IV(DATA, TYPE_VAR);

PROC SQL;

	SELECT COUNT(*) INTO :TOTAL TRIMMED FROM VARIABLELIST WHERE TYPE = "&TYPE_VAR.";
	SELECT VARIABLE INTO :VAR1-:VAR&TOTAL. FROM VARIABLELIST WHERE TYPE = "&TYPE_VAR.";

QUIT;

	%DO I = 1 %TO &TOTAL.;
	%PUT ------------ &I. OF &TOTAL.: &&VAR&I.;
		DATA TEMP;
		SET &DATA.(KEEP= DEFAULT_12M &&VAR&I.) ;
		RUN;

		%IF &TYPE_VAR. = NUM %THEN %DO;

			DATA TEMP;
			SET TEMP(RENAME=(&&VAR&I. = ORIG_&&VAR&I.));
			RUN;

			PROC HPBIN DATA=TEMP OUTPUT=TEMP NUMBIN=10 PSEUDO_QUANTILE;
				INPUT ORIG_&&VAR&I. ;            
				ID DEFAULT_12M;
			RUN;

			DATA TEMP;
			SET TEMP(RENAME=(BIN_ORIG_&&VAR&I. = &&VAR&I.));
			RUN;
		%END;

PROC SQL;
	SELECT COUNT(*) - SUM(DEFAULT_12M), SUM(DEFAULT_12M) INTO :NUM_GOOD_TOTAL, :NUM_BAD_TOTAL FROM TEMP;

	CREATE TABLE WOE_IV AS
	SELECT
		B.*
		, SUM(WOE_DISTR_GOOD_BAD) AS IV
	FROM (
		SELECT
			A.*
			, LOG(DISTR_GOOD/DISTR_NUM_BAD) AS WoE
			, (DISTR_GOOD - DISTR_NUM_BAD) AS DISTR_GOOD_BAD
			, (DISTR_GOOD - DISTR_NUM_BAD) * LOG(DISTR_GOOD/DISTR_NUM_BAD) AS WOE_DISTR_GOOD_BAD
		FROM (
				SELECT
					&&VAR&I.
					, COUNT(*) AS NUM_TOTAL
					, COUNT(*) - SUM(DEFAULT_12M) AS NUM_GOOD
					, SUM(DEFAULT_12M) AS NUM_BAD
					, (COUNT(*) - SUM(DEFAULT_12M))/&NUM_GOOD_TOTAL. AS DISTR_GOOD
					, (SUM(DEFAULT_12M))/&NUM_BAD_TOTAL. AS DISTR_NUM_BAD
				FROM TEMP
				GROUP BY &&VAR&I.
			) A
		)B
	;
QUIT;

	%IF &TYPE_VAR. = CAT %THEN %DO;
		DATA WOE_IV;
		SET WOE_IV;
			LENGTH VARIABLE $ 50;
			VARIABLE = CAT("&&VAR&I.", "__", &&VAR&I.);
		RUN;
	%END;

	%IF &TYPE_VAR. = NUM OR &TYPE_VAR. = IND %THEN %DO;
		DATA WOE_IV;
		SET WOE_IV(OBS=1);
			LENGTH VARIABLE $ 50;
			VARIABLE = "&&VAR&I.";
			WOE = .;
		RUN;
	%END;

		DATA WOE_IV;
		SET WOE_IV;

		KEEP VARIABLE WOE IV;

		RUN;

		DATA WOE_IV_FIN;
		SET WOE_IV_FIN WOE_IV;
			IF VARIABLE = '' THEN DELETE;
		RUN;

	%END;

	PROC SQL; DROP TABLE TEMP; QUIT;

%MEND;

DATA WOE_IV_FIN;
LENGTH GROUP $ 5 VARIABLE $ 50 LABEL $ 150;
RUN;

%WOE_IV(DATA=&USED_DATASET., TYPE_VAR=CAT)
%WOE_IV(DATA=&USED_DATASET., TYPE_VAR=NUM)
%WOE_IV(DATA=&USED_DATASET., TYPE_VAR=IND)

DATA plot.WOE_IV_FIN;
SET WOE_IV_FIN;
GROUP = 'ALL';
RUN;